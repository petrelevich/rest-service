plugins {
    id 'idea'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.springframework.boot' version '3.0.0' apply false
    id 'com.google.cloud.tools.jib' version '3.3.1' apply false
    id 'io.freefair.lombok' version '6.4.2' apply false
    id "fr.brouillard.oss.gradle.jgitver" version "0.10.0-rc03" apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.petrelevich"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:3.0.0")
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }

    task managedVersions {
        doLast {
            dependencyManagement.managedVersions.each {
                println it
            }
        }
    }
}
